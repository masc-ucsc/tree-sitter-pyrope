
dispatch = fun (a,b,c) {
  $(comptime) assert a equals string
  ret match a {
    == 'add' { b + c }
    == 'sub' { b - c }
    == 'mul' { b * c }
    == 'div' { b / c }
  }
}

dispatch2 = fun <T>(a:string,b:T,c:T) {
  s <- (
    ,add = fun[b,c] () { b + c }
    ,sub = fun[b,c] () { b - c }
    ,mul = fun[b,c] () { b * c }
    ,div = fun[b,c] () { b / c }
  )
  ret s[a]() // a,b are captured
}

dispatch3 = fun <T>(a:string,b:T,c:T) {
  s <- (
    ,add = fun (x,y) { ret x + y }
    ,sub = fun (x,y) { ret x - y }
    ,mul = fun (x,y) { ret x * y }
    ,div = fun (x,y) { ret x / y }
  )
  ret s[a](b,c)
}

test "Check methods equivalence" {

  for i in 0..<100 {
    a = i5.rand()
    b = i44.rand()

    {
      x <- dispatch('add',a,b)
      y <- dispatch2('add',a,b)
      z <- dispatch3('add',a,b)
      assert x == y == z == (a+b)
    }

    {
      x := dispatch('sub',a,b)
      y := dispatch2('sub',a,b)
      z := dispatch3('sub',a,b)
      assert x == y == z == (a-b)
    }

    {
      x <- dispatch('mul',a,b)
      y <- dispatch2('mul',a,b)
      z <- dispatch3('mul',a,b)
      assert x == y == z == (a*b)
    }

    {
      x <- dispatch('div',a,b)
      y <- dispatch2('div',a,b)
      z <- dispatch3('div',a,b)
      assert x == y == z == (a/b)
    }
  }
}
